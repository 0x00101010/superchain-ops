set dotenv-load

export rpcUrl := env_var('ETH_RPC_URL')
export superchainConfigAddr := env_var('SUPERCHAIN_CONFIG_ADDR')
export safeAddr := env_var('SAFE_ADDR')
export location := `pwd`

jsonFile := ''

######################################
# During the ceremony ...
######################################

install: install-presigner

install-presigner:
    #!/usr/bin/env bash
    go install github.com/ethereum-optimism/presigner@v0.0.3

whoami hdPath='0':
    #!/usr/bin/env bash
    if [ -z "$SIMULATE_WITHOUT_LEDGER" ]; then
      eip712sign --address --hd-paths "m/44'/60'/{{hdPath}}'/0/0" --ledger
    else
      eip712sign --address --mnemonic "test test test test test test test test test test test junk"
    fi

sign hdPath jsonFile:
    #!/usr/bin/env bash
    set -x
    export INPUT_JSON_PATH="${location}/input.json"
    JSON_FILE=`realpath {{jsonFile}}`
    if [ -z "$SIMULATE_WITHOUT_LEDGER" ]; then
      presigner \
        --workdir . \
        --script-name SignFromInputJson \
        --rpc-url ${rpcUrl} \
        --json-file $JSON_FILE \
        --ledger \
        --hd-paths "m/44'/60'/{{hdPath}}'/0/0" \
        sign
    else
      presigner \
        --workdir . \
        --script-name SignFromInputJson \
        --rpc-url ${rpcUrl} \
        --json-file $JSON_FILE \
        --mnemonic "test test test test test test test test test test test junk" \
        --sender 0xE7dEA1306D9F829bA469d1904c50903b46ebd02e \
        sign
    fi

######################################
# Before the ceremony ...
######################################

prepare:
  #!/usr/bin/env bash
  set -x
  echo creating 3 tx...
  echo

  export INPUT_JSON_PATH="${location}/input.json"
  mkdir -p ${location}/tmp
  for nonce in 92 93 94
  do
    presigner \
      --workdir ${location} \
      --script-name SignFromInputJson \
      --chain 1 \
      --rpc-url ${rpcUrl} \
      --target-addr ${superchainConfigAddr} \
      --safe-addr ${safeAddr} \
      --safe-nonce ${nonce} \
      --json-file ${location}/tmp/tmp-${nonce}.json \
      create
    cat ${location}/tmp/tmp-${nonce}.json | jq . > ${location}/tx/draft-${nonce}.json
  done

######################################
# After the ceremony ...
######################################

merge jsonFile +signedFiles:
  #!/usr/bin/env bash
  JSON_FILE=`realpath {{jsonFile}}`
  presigner \
    --rpc-url ${rpcUrl} \
    --json-file $JSON_FILE \
    merge {{signedFiles}}

verify jsonFile:
  #!/usr/bin/env bash
  JSON_FILE=`realpath {{jsonFile}}`
  presigner \
    --workdir . \
    --script-name SignFromInputJson \
    --rpc-url ${rpcUrl} \
    --json-file $JSON_FILE \
    verify

simulate jsonFile:
  #!/usr/bin/env bash
  JSON_FILE=`realpath {{jsonFile}}`
  presigner \
    --workdir . \
    --script-name SignFromInputJson \
    --rpc-url ${rpcUrl} \
    --json-file $JSON_FILE \
    simulate
