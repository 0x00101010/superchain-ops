set dotenv-load

export rpcUrl := env_var('ETH_RPC_URL')
export superchainConfigAddr := env_var('SUPERCHAIN_CONFIG_ADDR')
export safeAddr := env_var('SAFE_ADDR')
export location := `pwd`

jsonFile := ''

######################################
# During the ceremony ...
######################################

install: install-presigner

install-presigner:
    #!/usr/bin/env bash
    go install github.com/ethereum-optimism/presigner@v0.0.2

whoami hdPath='0':
    #!/usr/bin/env bash
    REPO_ROOT=`git rev-parse --show-toplevel`
    PATH="$REPO_ROOT/bin:$PATH"
    $REPO_ROOT/bin/eip712sign --address --hd-paths "m/44'/60'/{{hdPath}}'/0/0" --ledger

sign hdPath jsonFile: install
    #!/usr/bin/env bash
    set -x
    REPO_ROOT=`git rev-parse --show-toplevel`
    PATH="$REPO_ROOT/bin:$PATH"
    JSON_FILE=`realpath {{jsonFile}}`
    cd $REPO_ROOT/lib/presigner
    if [ -z "$SIMULATE_WITHOUT_LEDGER" ]; then
      presigner \
        --rpc-url ${rpcUrl} \
        --json-file $JSON_FILE \
        --ledger \
        --hd-paths "m/44'/60'/{{hdPath}}'/0/0" \
        sign
    else
      presigner \
        --rpc-url ${rpcUrl} \
        --json-file $JSON_FILE \
        --mnemonic "test test test test test test test test test test test junk" \
        --sender 0xE7dEA1306D9F829bA469d1904c50903b46ebd02e \
        sign
    fi

######################################
# After the ceremony ...
######################################

merge jsonFile +signedFiles:
  #!/usr/bin/env bash
  REPO_ROOT=`git rev-parse --show-toplevel`
  PATH="$REPO_ROOT/bin:$PATH"
  JSON_FILE=`realpath {{jsonFile}}`
  presigner \
    --rpc-url ${rpcUrl} \
    --json-file $JSON_FILE \
    merge {{signedFiles}}

verify jsonFile:
  #!/usr/bin/env bash
  REPO_ROOT=`git rev-parse --show-toplevel`
  PATH="$REPO_ROOT/bin:$PATH"
  JSON_FILE=`realpath {{jsonFile}}`
  cd $REPO_ROOT/lib/presigner
  presigner \
    --rpc-url ${rpcUrl} \
    --json-file $JSON_FILE \
    verify

simulate jsonFile:
  #!/usr/bin/env bash
  REPO_ROOT=`git rev-parse --show-toplevel`
  PATH="$REPO_ROOT/bin:$PATH"
  JSON_FILE=`realpath {{jsonFile}}`
  cd $REPO_ROOT/lib/presigner
  presigner \
    --rpc-url ${rpcUrl} \
    --json-file $JSON_FILE \
    simulate
